
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Перем СообщениеОбОшибке, ДополнительноеСообщение;
	
	УчетнаяЗапись = ПараметрКоманды;
	
	Если ТипЗнч(УчетнаяЗапись) = Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
		
		ОчиститьСообщения();
		
		Если ПарольЗадан(УчетнаяЗапись) Тогда
			ПарольПараметр = Неопределено;
		Иначе
			ПараметрУчетнаяЗапись = Новый Структура("УчетнаяЗапись", УчетнаяЗапись);
			ПарольПараметр = ОткрытьФормуМодально("ОбщаяФорма.ПодтверждениеПароляУчетнойЗаписи", ПараметрУчетнаяЗапись);
			Если ТипЗнч(ПарольПараметр) <> Тип("Строка") Тогда
				Возврат
			КонецЕсли;
		КонецЕсли;
		
		ПроверитьВозможностьОтправкиИПолученияЭлектроннойПочты(УчетнаяЗапись, ПарольПараметр, СообщениеОбОшибке, ДополнительноеСообщение);
		
		Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
			СообщитьОбОшибке(СообщениеОбОшибке);
		Иначе
			Предупреждение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			                 НСтр("ru = 'Проверка параметров учетной записи завершилась успешно!%1'"),
			                 ДополнительноеСообщение ),,
			                НСтр("ru = 'Проверка учетной записи'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбОшибке(ТекстСообщенияОшибки)
	
	Предупреждение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Проверка параметров учетной записи завершилась с ошибками:
							   |%1'"), ТекстСообщенияОшибки ),,
					НСтр("ru = 'Проверка учетной записи'"));
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьОтправкиИПолученияЭлектроннойПочты(УчетнаяЗапись, ПарольПараметр, СообщениеОбОшибке, ДополнительноеСообщение)
	
	СообщениеОбОшибке = "";
	ДополнительноеСообщение = "";
	
	Если УчетнаяЗапись.ИспользоватьДляОтправки Тогда
		Попытка
			ПроверитьВозможностьОтправкиТестовогоСообщения(УчетнаяЗапись, ПарольПараметр);
		Исключение
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								  НСтр("ru = 'Ошибка при отправке сообщения: %1'"),
								  ОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеОписанияОшибки(ИнформацияОбОшибке()) );
		КонецПопытки;
	Иначе
		ДополнительноеСообщение = Символы.ПС + НСтр("ru = 'Прим. учетная запись не используется для отправки электронных сообщений.'");
	КонецЕсли;
	
	Если УчетнаяЗапись.ИспользоватьДляПолучения Тогда
		Попытка
			ПроверитьВходНаСерверВходящейЭлектроннойПочты(УчетнаяЗапись, ПарольПараметр);
		Исключение
			Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
				СообщениеОбОшибке = СообщениеОбОшибке + Символы.ПС;
			КонецЕсли;
			
			СообщениеОбОшибке = СообщениеОбОшибке
								+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										 НСтр("ru = 'Ошибка доступа к серверу входящих сообщений: %1'"),
										 ОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеОписанияОшибки(ИнформацияОбОшибке()) );
		КонецПопытки;
	Иначе
		ДополнительноеСообщение = ДополнительноеСообщение
								+ Символы.ПС
								+ НСтр("ru = 'Прим. учетная запись не используется для получения электронных сообщений.'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Процедура проверяющая возможность отправления почтового сообщения через
// учетную запись
//
// Парамеры
// УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись,
//                 которую необходимо проверить
//
// Возвращаемое значение
// структура 
// ключ "статус" - булево, если Истина - успешно вошли на сервер pop3
//                 если Ложь - ошибка при входе на сервер pop3
// ключ "СообщениеОбОшибке" - строка - если статус Ложь - содержит сообщение об ошибке
//
Процедура ПроверитьВозможностьОтправкиТестовогоСообщения(знач УчетнаяЗапись,
                                                         знач Пароль = Неопределено)
	
	ПараметрыПисьма = Новый Структура;
	
	ПараметрыПисьма.Вставить("Тема", НСтр("ru = 'Тестовое сообщение 1С:Предприятие'"));
	ПараметрыПисьма.Вставить("Тело", НСтр("ru = 'Это сообщение отправлено подсистемой электронной почты 1С:Предприятие'"));
	ПараметрыПисьма.Вставить("Кому", УчетнаяЗапись.АдресЭлектроннойПочты);
	Если Пароль <> Неопределено Тогда
		ПараметрыПисьма.Вставить("Пароль", Пароль);
	КонецЕсли;
	
	ЭлектроннаяПочта.ОтправитьПочтовоеСообщение(УчетнаяЗапись, ПараметрыПисьма);
	
КонецПроцедуры

&НаСервере
Функция ПарольЗадан(УчетнаяЗапись)
	
	Возврат ЗначениеЗаполнено(УчетнаяЗапись.Пароль);
	
КонецФункции

&НаСервере
// Процедура проверяющая возможность получения почтового сообщения через
// учетную запись
//
// Парамеры
// УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись,
//                 которую необходимо проверить
//
// Возвращаемое значение
// структура 
// ключ "статус" - булево, если Истина - успешно вошли на сервер pop3
//                 если Ложь - ошибка при входе на сервер pop3
// ключ "СообщениеОбОшибке" - строка - если статус Ложь - содержит сообщение об ошибке
//
Процедура ПроверитьВходНаСерверВходящейЭлектроннойПочты(знач УчетнаяЗапись,
                                                        знач Пароль = Неопределено) Экспорт
	
	ПараметрыЗагрузки = Новый Структура("РежимТестирования", Истина);
	
	Если Пароль <> Неопределено Тогда
		ПараметрыЗагрузки.Вставить("Пароль", Пароль);
	КонецЕсли;
	
	ЭлектроннаяПочта.ЗагрузитьПочтовыеСообщения(УчетнаяЗапись, ПараметрыЗагрузки);
	
КонецПроцедуры
