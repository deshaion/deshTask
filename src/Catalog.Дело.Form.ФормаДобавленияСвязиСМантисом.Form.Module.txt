/////////////////////////////////////////////////////////////////////////////////////////////
//////////////// События формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Параметры.Дело) Тогда Отказ = Истина; Возврат; КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Дело = Параметры.Дело;
	КодМантиса = ПолучитьКодМантиса(Дело);
	
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка) 
	Если Модифицированность Тогда
		Ответ = Вопрос("Вы уверенны, что хотите закрыть форму без сохранения данных?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////////////////
//////////////// События элементов формы

&НаКлиенте
Процедура ПолеРедактированияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	ОбработатьПолеРедактирования(Текст);
	Ожидание = 0;
	ПолеРедактирования = "";
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////////////////
//////////////// Команды

&НаКлиенте
Процедура Записать(Команда)
	ЗаписьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если ЗаписьНаКлиенте() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////////////////
//////////////// Вспомогательные процедуры

&НаКлиенте 
Функция ЗаписьНаКлиенте()
	
	Если НЕ ЗначениеЗаполнено(НовыйКодВМантисе) Тогда
		Отказ = Истина;
		Сообщить(НСтр("ru = ""Невозможно записать связь, т.к. не указан новый код обращения в мантисе."""));
	КонецЕсли;
	
	ЗаписьНаСервере();
	
	Возврат Истина;
КонецФункции

&НаСервере 
Процедура ЗаписьНаСервере()
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		тмпЗапись = РегистрыСведений.МантисЗадачи.СоздатьМенеджерЗаписи();
		тмпЗапись.Дело = Дело;
		Если ЗначениеЗаполнено(КодМантиса) Тогда
			тмпЗапись.КодМантиса = КодМантиса;
			тмпЗапись.Прочитать();
		КонецЕсли;
		тмпЗапись.КодМантиса = НовыйКодВМантисе;	
		тмпЗапись.Записать();
		Модифицированность = Ложь;
		КодМантиса = НовыйКодВМантисе;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолеРедактирования(Знач Текст)  
	Если ЗначениеЗаполнено(Текст) Тогда
		МассивНомеров = Новый Массив;
		СтрокаЦифр = "0123456789";
		Пока Истина Цикл
			тНомерВМантисе = "";
			сч = СтрДлина(Текст);    
			Пока сч > 0 Цикл
				тСимвол = Сред(Текст, сч, 1); 
				Вхождение = Найти(СтрокаЦифр, тСимвол);
				Если Вхождение = 0 И СтрДлина(тНомерВМантисе) > 0 Тогда
					Прервать;
				ИначеЕсли Вхождение > 0 Тогда
					тНомерВМантисе = тСимвол+тНомерВМантисе;
				КонецЕсли;
				сч = сч -1;
			КонецЦикла;
			Если тНомерВМантисе = "" Тогда
				Прервать;
			Иначе
				МассивНомеров.Добавить(тНомерВМантисе);
				Текст = СтрЗаменить(Текст, тНомерВМантисе, "");
			КонецЕсли;
		КонецЦикла;
		НомерВМантисе = "";
		Для Каждого тНомерВМантисе Из МассивНомеров Цикл
			Если СтрДлина(тНомерВМантисе) > СтрДлина(НомерВМантисе) Тогда
				НомерВМантисе = тНомерВМантисе;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(НомерВМантисе) Тогда
			НовыйКодВМантисе = НомерВМантисе;
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьКодМантиса(пДело)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МантисЗадачи.КодМантиса
	|ИЗ
	|	РегистрСведений.МантисЗадачи КАК МантисЗадачи
	|ГДЕ
	|	МантисЗадачи.Дело = &Дело";
	
	Запрос.УстановитьПараметр("Дело", пДело);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КодМантиса;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции


