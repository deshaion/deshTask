
&НаКлиенте
Процедура ГруппаРодительПриИзменении(Элемент)
	Элементы.Список.РодительВерхнегоУровня = ГруппаРодитель;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.Список.ОтображатьКорень = Ложь;
	Элементы.Список.РодительВерхнегоУровня = ГруппаРодитель;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ОтборПоЗадаче) Тогда
		элементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		элементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СсылкаНаЗадачу");
		элементОтбора.ПравоеЗначение = Параметры.ОтборПоЗадаче;

		// это свойство указывает на то, что данный отбор будет не доступен для изменения
		элементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиКВыполнению(Команда)
	Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	текДело = Элементы.Список.ТекущиеДанные;
	
	Если текДело = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДела = Новый Структура("Ссылка", текДело.Ссылка);
	
	ПеренестиКВыполнениюСервер(ПараметрыДела);
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Функция ПеренестиКВыполнениюСервер(пПараметры)
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПланРазвития.Ссылка,
	|	ПланРазвития.Наименование КАК ИмяДела,
	|	ПланРазвития.СсылкаНаЗадачу
	|ИЗ
	|	Справочник.ПланРазвития КАК ПланРазвития
	|ГДЕ
	|	ПланРазвития.Ссылка В ИЕРАРХИИ(&Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка", пПараметры.Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.СсылкаНаЗадачу) Тогда
			ПараметрыДела = Новый Структура("Ссылка, Дело, ДатаДобавления, ПереноситьКомментарии", Выборка.Ссылка, Выборка.СсылкаНаЗадачу, ТекущаяДата(), Ложь);
		Иначе
			ПараметрыДела = Новый Структура("Ссылка, Дело, ДатаДобавления, ПереноситьКомментарии", Выборка.Ссылка, МодульДел.СоздатьНовоеДело(Выборка.ИмяДела), ТекущаяДата(), Истина);
			
			объектРазвития = ПараметрыДела.Ссылка.ПолучитьОбъект();
			объектРазвития.СсылкаНаЗадачу = ПараметрыДела.Дело;
			объектРазвития.Записать();
		КонецЕсли;
		
		ЗадачиКВыполнению.ПеренестиКВыполнениюСервер(ПараметрыДела);
	КонецЦикла;
КонецФункции // ПеренестиКВыполнениюСервер()
