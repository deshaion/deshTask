
&НаКлиенте
Процедура УскоритьВыполнение(Команда)
	текДанные = Элементы.Список.ТекущиеДанные;
	Если текДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Голоса.СдвинутьВверх(текДанные.Дело);
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗатормозитьВыполнение(Команда)
	текДанные = Элементы.Список.ТекущиеДанные;
	Если текДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Голоса.СдвинутьВниз(текДанные.Дело);
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКрайнийСрок(Команда)
	текДанные = Элементы.Список.ТекущиеДанные;
	Если текДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	лКрайнийСрок = ТекущаяДата() + 5 * 24 * 3600;
	Если ВвестиДату(лКрайнийСрок, "Введите крайний срок", ЧастиДаты.Дата) Тогда
    	Голоса.УстановитьКрайнийСрок(текДанные.Дело, лКрайнийСрок, текСотрудник);
	КонецЕсли;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ПараметрыСеанса.РольСотрудника = Перечисления.РолиСотрудников.ПолныеПрава Тогда
		текСотрудник = ПараметрыСеанса.ТекущийСотрудник;
	
		Отбор = Список.Отбор.Элементы;
		
		условиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		условиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Голос");
		условиеОтбора.видСравнения = ВидСравненияКомпоновкиДанных.Равно;
		условиеОтбора.ПравоеЗначение = текСотрудник;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьДело(пКлюч)
	МЗ = РегистрыСведений.ГолосаДел.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МЗ, пКлюч);
	
	Возврат МЗ.Дело;
КонецФункции // ПолучитьДело()

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Поле = Элементы.КрайнийСрок Тогда
		УстановитьКрайнийСрок(Неопределено);
		Возврат;
	КонецЕсли;
	
	выбДело = ПолучитьДело(ВыбраннаяСтрока);
	
	ОткрытьФорму("Справочник.Дело.ФормаОбъекта", Новый Структура("Ключ", выбДело));
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПослеУдаления()
	Голоса.ВосстановитьПорядок();
КонецПроцедуры // ПослеУдаления()

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	ПослеУдаления();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьКонтролируемыеЗадачи" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура Обновить(Команда)
	Отбор = Список.Отбор.Элементы;
		
	ОтборУстановлен = Ложь;
	Для каждого лЭлементОтбора Из Отбор Цикл
		Если лЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Голос") Тогда
			лЭлементОтбора.ПравоеЗначение = текСотрудник;
			ОтборУстановлен = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ОтборУстановлен Тогда
		условиеОтбора = Отбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		условиеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Голос");
		условиеОтбора.видСравнения = ВидСравненияКомпоновкиДанных.Равно;
		условиеОтбора.ПравоеЗначение = текСотрудник;
	КонецЕсли;
КонецПроцедуры

