
Функция СохранитьГолос(пДело, пСотрудник = Неопределено) Экспорт 
	Если НЕ ЗначениеЗаполнено(пДело) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если пСотрудник = Неопределено Тогда
		пСотрудник = ПараметрыСеанса.ТекущийСотрудник;
	КонецЕсли;
	
	наборЗаписей = РегистрыСведений.ГолосаДел.СоздатьНаборЗаписей();
	наборЗаписей.Отбор.Голос.Установить(пСотрудник);
	наборЗаписей.Прочитать();
	
	таблицаЗаписей = наборЗаписей.Выгрузить();
	
	строкаЗаписи = таблицаЗаписей.Найти(пДело, "Дело");
	
	Если строкаЗаписи = Неопределено Тогда
		новаяЗапись = таблицаЗаписей.Добавить();
		новаяЗапись.Дело = пДело;
		новаяЗапись.Голос = пСотрудник;
		новаяЗапись.СистемныйПриоритет = 9999999;
	Иначе
		таблицаЗаписей.Удалить(строкаЗаписи);
	КонецЕсли;
	
	ВосстановитьПорядок(таблицаЗаписей, пСотрудник);
	
	наборЗаписей.Загрузить(таблицаЗаписей);
	наборЗаписей.Записать();
	
	Возврат Истина;
КонецФункции // СохранитьГолос()

Функция ВосстановитьПорядок(пТаблица = Неопределено, пСотрудник = Неопределено) Экспорт 
	Если пСотрудник = Неопределено Тогда
		пСотрудник = ПараметрыСеанса.ТекущийСотрудник;
	КонецЕсли;
	
	Записать = Ложь;
	Если пТаблица = Неопределено Тогда
		наборЗаписей = РегистрыСведений.ГолосаДел.СоздатьНаборЗаписей();
		наборЗаписей.Отбор.Голос.Установить(пСотрудник);
		наборЗаписей.Прочитать();
	
		пТаблица = наборЗаписей.Выгрузить();
		Записать = Истина;
	КонецЕсли;
	
	пТаблица.Сортировать("СистемныйПриоритет");
	
	// перенумеруем голоса
	нумер = 1;
	Для каждого текЗапись Из пТаблица Цикл
		текЗапись.СистемныйПриоритет = нумер;
		нумер = нумер + 1;
	КонецЦикла;
	
	Если Записать Тогда
		наборЗаписей.Загрузить(пТаблица);
		наборЗаписей.Записать();
	КонецЕсли;
КонецФункции // ВосстановитьПорядок()

Функция Сдвинуть(пДело, пСотрудник, пНаправление = 1)
	Если НЕ ЗначениеЗаполнено(пДело) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если пСотрудник = Неопределено Тогда
		пСотрудник = ПараметрыСеанса.ТекущийСотрудник;
	КонецЕсли;
	
	наборЗаписей = РегистрыСведений.ГолосаДел.СоздатьНаборЗаписей();
	наборЗаписей.Отбор.Голос.Установить(пСотрудник);
	наборЗаписей.Прочитать();
	
	таблицаЗаписей = наборЗаписей.Выгрузить();
	таблицаЗаписей.Сортировать("СистемныйПриоритет");
	
	строкаЗаписи = таблицаЗаписей.Найти(пДело, "Дело");
	
	Если строкаЗаписи = Неопределено Тогда
		Возврат Ложь;
	Иначе
		колво = таблицаЗаписей.Количество();
		идСтроки = таблицаЗаписей.Индекс(строкаЗаписи);
		идСтрокиСвязь = идСтроки + пНаправление;
		Если идСтрокиСвязь < 0 ИЛИ идСтрокиСвязь >= колво Тогда
			Возврат Ложь;
		КонецЕсли;
		строкаСвязанная = таблицаЗаписей[идСтрокиСвязь];
		
		запомнить = строкаЗаписи.СистемныйПриоритет;
		строкаЗаписи.СистемныйПриоритет = строкаСвязанная.СистемныйПриоритет;
		строкаСвязанная.СистемныйПриоритет = запомнить;
	КонецЕсли;
	
	наборЗаписей.Загрузить(таблицаЗаписей);
	наборЗаписей.Записать();
КонецФункции // Сдвинуть()

Функция СдвинутьВверх(пДело, пСотрудник = Неопределено) Экспорт 
	Возврат Сдвинуть(пДело, пСотрудник, -1);
КонецФункции // СдвинутьВверх()

Функция СдвинутьВниз(пДело, пСотрудник = Неопределено) Экспорт 
	Возврат Сдвинуть(пДело, пСотрудник, 1);
КонецФункции // СдвинутьВверх()

Функция УстановитьКрайнийСрок(пДело, пКрайнийСрок, пСотрудник = Неопределено) Экспорт 
	Если НЕ ЗначениеЗаполнено(пДело) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если пСотрудник = Неопределено Тогда
		пСотрудник = ПараметрыСеанса.ТекущийСотрудник;
	КонецЕсли;
	
	наборЗаписей = РегистрыСведений.ГолосаДел.СоздатьНаборЗаписей();
	наборЗаписей.Отбор.Голос.Установить(пСотрудник);
	наборЗаписей.Отбор.Дело.Установить(пДело);
	наборЗаписей.Прочитать();
	
	Если наборЗаписей.Количество() Тогда
		текЗапись = наборЗаписей[0];
		
		текЗапись.КрайнийСрок = пКрайнийСрок;
		
		наборЗаписей.Записать();
	КонецЕсли;
	
	Возврат Истина;
КонецФункции // УстановитьКрайнийСрок()